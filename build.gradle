apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'
apply plugin: 'java'

project.version = '0.1'
group = 'fr.islandswars'
def projectName = 'islands-ineundo'

sourceCompatibility = 1.10
targetCompatibility = 1.10

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.compilerArgs = ['-parameters']

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
    compile 'net.md-5:bungeecord-api:1.12-SNAPSHOT'
    testCompile 'junit:junit:4.9'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.+')
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name, 'Implementation-Version': version
    }
}

task fatJar(type: Jar) {
    archiveName = projectName + '.jar'
    from { (configurations.runtime - configurations.provided).collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


artifactory {
    def branch = "$System.env.TRAVIS_BRANCH"
    println 'Build on branch : ' + branch
    if (branch != 'master')
        version += '-' + branch

    publish {
        contextUrl = 'http://repo.islandswars.fr:8081/artifactory'
        repository {
            repoKey = 'islandswars'
            username = System.env.ARTIFACTORY_USER
            password = System.env.ARTIFACTORY_PASSWORD
        }
        defaults {
            publications('mavenJava')
        }
    }

}

javadoc {
    doFirst {
        options.setWindowTitle('IslandsWars')
        options.setDocTitle('IslandsWars')
        options.setUse(true)
        options.setVersion(true)
        options.setAuthor(true)
        options.addBooleanOption('html5', true)
    }
    destinationDir = file("../docs")
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact fatJar

            artifact javadocJar

            artifact sourceJar
        }
    }
}
